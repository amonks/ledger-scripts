#!/usr/bin/perl
#
# getquote
#
# Get quotes for various markets with a focus on cryptocurrencies
#
# Markets:
#
# BitcoinAverage (BTC/USD)
# Coinabul (Gold, Silver)
# Havelock Investments
# Cryptostocks
# NASDAQ
#

use strict;

use Finance::Quote;
use HTTP::Request;
use LWP::UserAgent;
use JSON;
use POSIX qw(strftime);

my $timeout = 60;

my $symbol = $ARGV[0];
my $price;

if (!$symbol) {
    print STDERR "Usage: getquote TICKER\n";
    exit 1;
}

sub json_req {
    my $url = shift;
    print STDERR "Looking up $symbol\n";
    my $req = HTTP::Request->new('GET', $url);
    my $ua = LWP::UserAgent->new(agent => 'perl/' . $^V);
    $ua->timeout($timeout);
    $ua->ssl_opts(verify_hostname => 0);
    my $res = $ua->request($req);
    if ($res->is_success) {
        return decode_json($res->decoded_content);
    }
}

if ($symbol eq '$') {
    if (my $ticker = json_req('https://api.bitcoinaverage.com/ticker/USD')) {
        $price = sprintf '%.8f BTC', 1/$ticker->{last}
    }
}

if ($symbol eq 'BTC') {
    if (my $ticker = json_req('https://api.bitcoinaverage.com/ticker/USD')) {
        $price = '$' . $ticker->{last};
    }
}

my %coinabul_markets = (
    AU => 'Gold',
    AG => 'Silver',
);

my %cryptotrade_markets = (
    CTB => 'BTC',
    CTL => 'LTC',
    ESB => 'BTC',
    ESL => 'LTC',
    GGB => 'BTC',
    PPC => 'BTC',
    LTC => 'BTC',
    NMC => 'BTC',
    XPM => 'BTC',
    FTC => 'BTC',
);

my %cryptostocks_markets = (
    BTCINVEST => 1,
    GREEN => 1,
);

if (my $marketid = $coinabul_markets{$symbol}) {
    if (my $market = json_req('http://coinabul.com/api.php')) {
        $price = '$' . $market->{$marketid}->{USD};
    }
}

if (!$price and my $currency = $cryptotrade_markets{$symbol}) {
    my $ticker = lc($symbol . '_' . $currency);
    my $resp = json_req('https://crypto-trade.com/api/1/ticker/' . $ticker);
    if ($resp and $resp->{status} eq 'success') {
        my $data = $resp->{data};
        $price = $data->{last} || ($data->{min_ask} + $data->{max_bid}) / 2;
        $price .= ' ' . $currency;
    }
}

if (!$price and $cryptostocks_markets{$symbol}) {
    if (my $info = json_req('https://cryptostocks.com/api/get_security_info.json?ticker=' . $symbol)) {
        if ($info and $info->{return_code} == 0) {
            $price = $info->{last_price} . ' ' . $info->{currency};
        }
    }
}

if (!$price) {
    if ($symbol eq 'AM') {
        $symbol = 'AM1';
    }
    if (my $ticker = json_req('https://www.havelockinvestments.com/r/ticker')) {
        if (my $details = $ticker->{$symbol}) {
            $price = $details->{last} . ' BTC';
        }
    }
}

if (!$price) {
    print STDERR "Looking up $symbol (NASDAQ)\n";
    my $q = Finance::Quote->new;
    $q->timeout($timeout);
    $q->require_labels(qw/price/);
    my %quotes = $q->fetch("nasdaq", $symbol);
    if ($quotes{$symbol}) {
        $price = '$' . $quotes{$symbol, "price"};
    }
}

if ($price) {
    print strftime("%Y/%m/%d %H:%M:%S ", localtime(time())),
        $symbol, ' ', $price, "\n";
} else {
    exit 1;
}
