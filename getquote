#!/usr/bin/perl
#
# getquote
#
# Get quotes for various markets with a focus on cryptocurrencies
#
# Markets:
#
# BitcoinAverage (BTC/USD)
# Coinabul (Gold, Silver)
# Havelock Investments
# Cryptostocks
# Google Finance
# Rate-Exchange
#

use strict;

use Finance::Quote;
use HTTP::Request;
use LWP::UserAgent;
use JSON;
use POSIX qw(strftime);

my $timeout = 60;

my $symbol = $ARGV[0];
my $price;

if (!$symbol) {
    print STDERR "Usage: getquote TICKER\n";
    exit 1;
}

sub request {
    my $url = shift;
    print STDERR "Looking up $symbol\n";
    my $req = HTTP::Request->new('GET', $url);
    my $ua = LWP::UserAgent->new(agent => 'perl/' . $^V);
    $ua->timeout($timeout);
    $ua->ssl_opts(verify_hostname => 0);
    my $res = $ua->request($req);
    return $res->is_success ? $res->decoded_content : undef;
}

sub json_req {
    my $content = request(shift);
    return $content ? decode_json($content) : undef;
}

if ($symbol eq '$' or $symbol eq 'USD') {
    if (my $ticker = json_req('https://api.bitcoinaverage.com/ticker/USD')) {
        $price = sprintf '%.8f BTC', 1/$ticker->{last}
    }
}

if ($symbol eq 'BTC') {
    if (my $ticker = json_req('https://api.bitcoinaverage.com/ticker/USD')) {
        $price = '$' . $ticker->{last};
    }
}

my %coinabul_markets = (
    AU => 'Gold',
    AG => 'Silver',
);

my %cryptotrade_markets = (
    CTB => 'BTC',
    CTL => 'LTC',
    ESB => 'BTC',
    ESL => 'LTC',
    GGB => 'BTC',
    PPC => 'BTC',
    LTC => 'BTC',
    NMC => 'BTC',
    XPM => 'BTC',
    FTC => 'BTC',
);

my %cryptostocks_markets = (
    BTCINVEST => 1,
    GREEN => 1,
);

my %havelock_markets = (
    KCIM => 1,
    AM => 'AM1',
    HIF => 1,
    MS => 1,
    RENT => 1,
    SFI => 1,
    VTX => 1
);

my %rateexchange_markets = (
    'â‚¬' => 'EUR',
    EUR => 1
);

if (my $marketid = $coinabul_markets{$symbol}) {
    if (my $market = json_req('http://coinabul.com/api.php')) {
        $price = '$' . $market->{$marketid}->{USD};
    }
}

if (!$price and my $currency = $cryptotrade_markets{$symbol}) {
    my $ticker = lc($symbol . '_' . $currency);
    my $resp = json_req('https://crypto-trade.com/api/1/ticker/' . $ticker);
    if ($resp and $resp->{status} eq 'success') {
        my $data = $resp->{data};
        $price = $data->{last} || ($data->{min_ask} + $data->{max_bid}) / 2;
        $price .= ' ' . $currency;
    }
}

if (!$price and $cryptostocks_markets{$symbol}) {
    if (my $info = json_req('https://cryptostocks.com/api/get_security_info.json?ticker=' . $symbol)) {
        if ($info and $info->{return_code} == 0) {
            $price = $info->{last_price} . ' ' . $info->{currency};
        }
    }
}

if (!$price and $havelock_markets{$symbol}) {
    if ($havelock_markets{$symbol} != 1) {
        $symbol =$havelock_markets{$symbol};
    }
    if (my $ticker = json_req('https://www.havelockinvestments.com/r/ticker')) {
        if (my $details = $ticker->{$symbol}) {
            $price = $details->{last} . ' BTC';
        }
    }
}

if (!$price and $rateexchange_markets{$symbol}) {
    if ($rateexchange_markets{$symbol} != 1) {
        $symbol = $rateexchange_markets{$symbol};
    }
    if (my $info = json_req('http://rate-exchange.appspot.com/currency?to=USD&from=' . $symbol)) {
        if ($info and $info->{rate}) {
            $price = '$' . $info->{rate};
        }
    }
}

if (!$price) {
    if ($_ = request('http://www.google.com/finance/info?infotype=infoquoteall&q=' . $symbol)) {
        s/\/\/.*/[/;
        my $info = decode_json($_);
        if ($info and $info->[0]) {
            $price = '$' . $info->[0]->{l};
        }
    }
}

if ($price) {
    print strftime("%Y/%m/%d %H:%M:%S ", localtime(time())),
        $symbol, ' ', $price, "\n";
} else {
    exit 1;
}
