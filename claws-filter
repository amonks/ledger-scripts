# vi: syntax=perl
#
# Write ledger entries upon receiving emails about transactions

use strict;
use Date::Parse;
use POSIX qw(strftime);
use MIME::Base64;

sub write_debug {
	open FILE, '>', 'clawfilter.out';
	print FILE shift;
	close FILE;
}

sub write_entry {
	my ($payee, $amount, $account_to, $account_from) = @_;
	my $date = header('date');
	my $ledger_file = $ENV{LEDGER_FILE} // "Documents/Finances/journal.ledger";

	open LEDGER, '>>', $ledger_file
		or die("Failed to open journal $ledger_file: $! in $ENV{PWD}");
	$date = strftime "%Y/%m/%d", localtime(str2time($date));
	printf LEDGER "\n%s %s", $date, $payee;
	printf LEDGER "\n    %-36s%s", $account_to, $amount;
	printf LEDGER "\n    %s\n", $account_from;
	close LEDGER;
}

sub dividend_havelock {
	my $body = body();
	if (my ($ticker, $amount) = $body =~ /You have received a dividend payment from fund: (.*?)\n.*Total Dividend: (.*?) *(?:BTC)?\n/s) {
		write_entry "$ticker Dividend", "$amount BTC", "Assets:Broker:Havelock", "Income:Dividends";
		mark_as_read();
	}
}

sub dividend_cryptostocks {
	my $body = body();
	if (my ($ticker, $amount) = $body =~ /\((.*?)\) has paid a dividend .*? received a total dividend payment of (.*)/) {
		write_entry "$ticker Dividend", $amount, "Assets:Broker:Cryptostocks", "Income:Dividends";
		mark_as_read();
	}
}

sub payment_sent_paypal {
	my $body = decode_base64(body());
	if (my ($amount, $payee) = $body =~ /You sent a payment for (.*?) USD to (.*?)\./) {
		write_entry $payee, $amount, "Expenses", "Assets:Broker:Paypal";
		mark_as_read();
	}
}

sub payment_sent_dwolla {
	my ($base64) = body() =~ /\n\n(.*?)\n\n/s;
	return unless $base64;
	my $text = decode_base64($base64);
	if (my ($amount, $payee) = $text =~ /You have successfully sent (\$.*?) to (.*)\./) {
		write_entry $payee, $amount, "Expenses", "Assets:Broker:Dwolla";
		mark_as_read();
	}
}

my ($from) = extract_addresses(header('from'));
my ($subject) = header('subject');

dividend_havelock
	if ($from =~ /\@havelockinvestments\.com$/ &&
		$subject eq '[Havelock Investments] Dividend Received');

dividend_cryptostocks
	if ($from eq 'customerservice@cryptostocks.com' &
		$subject eq 'Dividend payment received');

payment_sent_paypal
	if ($from eq 'service@paypal.com' &&
		$subject eq 'You sent a payment');

payment_sent_dwolla
	if ($from eq 'no-reply@dwolla.com' &&
		$subject =~ /^You have sent money/);
