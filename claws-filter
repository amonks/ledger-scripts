# vi: syntax=perl
#
# Write ledger entries upon receiving emails about transactions

use strict;
use Date::Parse;
use POSIX qw(strftime);
use MIME::Base64;

sub write_debug {
    open FILE, '>', 'clawfilter.out';
    print FILE shift;
    close FILE;
}

sub write_entry {
    my ($payee, $amount, $account_to, $account_from) = @_;
    my $date = header('date');
    my $ledger_file = $ENV{LEDGER_FILE} // "/home/cel/Documents/Finances/journal.ledger";

    open LEDGER, '>>', $ledger_file
        or die("Failed to open journal $ledger_file: $! in $ENV{PWD}");
    $date = strftime "%Y/%m/%d", localtime(str2time($date));
    printf LEDGER "\n%s %s", $date, $payee;
    printf LEDGER "\n    %-32s  %14s", $account_to, $amount;
    printf LEDGER "\n    %s\n", $account_from;
    close LEDGER;
}

sub dividend_havelock {
    my $body = body();
    if (my ($ticker, $amount) = $body =~ /You have received a dividend payment from fund: (.*?)\n.*Total Dividend: (.*?) *(?:BTC)?\n/s) {
        write_entry "$ticker Dividend", "$amount BTC", "Assets:Broker:Havelock", "Income:Dividends";
        mark_as_read();
    }
}

sub dividend_cryptostocks {
    my $body = body();
    if (my ($ticker, $amount) = $body =~ /\((.*?)\) has paid a dividend .*? received a total dividend payment of (.*)/) {
        write_entry "$ticker Dividend", $amount, "Assets:Broker:Cryptostocks", "Income:Dividends";
        mark_as_read();
    }
}

sub payment_paypal {
    my $paypal_account = 'Assets:Broker:PayPal';
    my $body = body();
    if (header('Content-Transfer-Encoding') eq 'base64') {
        $body = decode_base64($body);
    }
    my $subject = header('Subject');
    my ($amount, $payee);

    if (($amount, $payee) = $body =~ /You sent a (?:mobile )?payment for (.*?) USD to (.*?)\./) {
        write_entry $payee, $amount, 'Expenses', $paypal_account;
        mark_as_read();

    } elsif (($amount, $payee) = $body =~ /Amount:=24(.*?) USD\s*To:(.*?)[\r\n]/) {
        write_entry $payee, '$'.$amount, 'Expenses', $paypal_account;

        mark_as_read();

    } elsif (($payee, $amount) = $subject =~ /(.*) sent you (.*) USD/) {
        my $description = $payee;
        if (my ($note) = $body =~ /[\r\n]Note from .*?:(.*)[\r\n]/) {
            $description .= "  ; $note";
        }
        write_entry $description, $amount, $paypal_account, 'Income';
        mark_as_read();
    }
}

sub payment_sent_dwolla {
    my ($base64) = body() =~ /\n\n(.*?)\n\n/s;
    return unless $base64;
    my $text = decode_base64($base64);
    if (my ($amount, $payee) = $text =~ /You have successfully sent (\$.*?) to (.*)\./) {
        write_entry $payee, $amount, "Expenses", "Assets:Broker:Dwolla";
        mark_as_read();
    }
}

sub payment_sent_coinbase {
    my $amount = shift;
    my ($description) = body() =~ /<p>Attached message:<\/p>\s*<blockquote>(.*?)<br>/s;
    $description ||= 'Withdraw from Coinbase';

    write_entry $description, $amount, "Expenses", "Assets:Bitcoin:Coinbase";
    mark_as_read();
}

sub payment_venmo {
    my $venmo_account = 'Assets:Broker:Venmo';
    my ($payee, $amount, $type) = @_;
    my ($note) = body() =~ /<!-- note -->\s*<div>\s*<p>(.*?)<\/p>/s;
    my $description = "$payee  ; $note";
    if ($type > 0) {
        write_entry $description, $amount, $venmo_account, 'Income';
    } else {
        write_entry $description, $amount, 'Expenses', $venmo_account;
    }
    mark_as_read();
}

my ($from) = extract_addresses(header('from'));
my ($subject) = header('subject');

dividend_havelock
    if ($from =~ /\@havelockinvestments\.com$/ &&
        $subject eq '[Havelock Investments] Dividend Received');

dividend_cryptostocks
    if ($from eq 'customerservice@cryptostocks.com' &
        $subject eq 'Dividend payment received');

payment_paypal
    if (($from eq 'service@paypal.com' && $subject =~ /^You sent/) ||
        ($from eq 'member@paypal.com' && $subject =~ /sent you/));

payment_sent_dwolla
    if ($from eq 'no-reply@dwolla.com' &&
        $subject =~ /^You have sent money/);

payment_sent_coinbase $1
    if ($from eq 'contact@coinbase.com' &&
        $subject =~ /^You just sent (.*? BTC) /);

if ($from eq 'venmo@venmo.com') {
    payment_venmo $1, $2, -1
        if ($subject =~ /^You paid (.*) (.*?)$/);

    payment_venmo $1, $2, 1
        if ($subject =~ /^(.*) paid you (.*?)$/);
}
